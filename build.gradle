plugins {
    id 'net.minecraftforge.gradle' version '5.1.+'
    id "org.spongepowered.mixin" version "0.7.+"
    id 'com.matthewprenger.cursegradle' version "${cursegradle_version}"
    id 'com.github.johnrengelman.shadow' version "${shadow_version}"
    id 'maven-publish'
}

var mod_version = mcef_version

archivesBaseName = project.archives_base_name
version = mod_version
group = mod_packagename

java {
    withJavadocJar()
    withSourcesJar()
    toolchain.languageVersion = JavaLanguageVersion.of(java_version as int)
}

def getCheckedOutGitCommitHash() {
    def gitFolder = "java-cef/.git/modules/common/java-cef/"
    def takeFromHash = 40
    /*
     * '.git/HEAD' contains either
     *      in case of detached head: the currently checked out commit hash
     *      otherwise: a reference to a file containing the current commit hash
     */
    def gitHeadFile = new File(gitFolder + "HEAD");
    if (gitHeadFile.exists()) {
        def head = gitHeadFile.text.split(":") // .git/HEAD
        def isCommit = head.length == 1 // e5a7c79edabbf7dd39888442df081b1c9d8e88fd
        // def isRef = head.length > 1     // ref: refs/heads/master

        if (isCommit) return head[0].trim().take(takeFromHash) // e5a7c79edabb

        def refHead = new File(gitFolder + head[1].trim()) // .git/refs/heads/master
        refHead.text.trim().take takeFromHash
    } else {
        return ""
    }
}


minecraft {
    mappings channel: 'official', version: minecraft_version

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
        }
        server {
            workingDirectory project.file('run/server')
            property 'forge.logging.console.level', 'debug'
            arg '--nogui'
        }
    }
}

mixin {
    add sourceSets.main, 'mcef.refmap.json'
    config 'mcef.mixins.json'
}

sourceSets {
    jcef {
        java {
            srcDir "java-cef/java"
            exclude "**/tests/**"
        }
    }

    main {
        compileClasspath += jcef.output
        runtimeClasspath += jcef.output
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

processResources.dependsOn(processJcefResources)

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

processResources {
    filesMatching('**/*.toml') {
        expand 'mod_id': mod_id,
                'mod_version': mod_version,
                'mod_name': mod_name,
                'forge_loader_requirement': forge_loader_requirement,
                'forge_requirement': forge_requirement,
                'minecraft_version': minecraft_version
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

jar {
    from sourceSets.jcef.output.classesDirs
    from sourceSets.jcef.output.resourcesDir

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes(['Specification-Title'     : mod_name,
                    'Specification-Vendor'    : mod_vendor,
                    'Specification-Version'   : "1",
                    'Implementation-Title'    : mod_name,
                    'Implementation-Version'  : mod_version,
                    'Implementation-Vendor'   : mod_vendor,
                    'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    'java-cef-commit'         : project.getCheckedOutGitCommitHash()
        ])
    }
}

tasks.register('deobfJar', Jar) {
    archiveClassifier = 'deobf'
    from sourceSets.main.output
}

jar.finalizedBy('reobfJar')

artifacts {
    archives deobfJar
}

publishing {
    publications {
        mcef(MavenPublication){
            from components.java
            artifactId "mcef"
        }

        mcefDeobf(MavenPublication){
            artifactId "mcef-deobf"
            artifact ("/build/libs/mcef-forge-" + version + "-deobf.jar")
            artifact sourcesJar
            artifact javadocJar
        }
    }

    repositories {
        maven {
            name = "repsy"
            credentials{
                username repsyUsername
                password repsyPassword
            }

            url = 'https://repo.repsy.io/mvn/javaracing/mcef'
        }
    }
}